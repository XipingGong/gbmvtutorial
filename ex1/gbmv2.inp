
! set up some common variables
! --------------------------------------------
if @?pdbid  eq 0  set pdbid  = 3gb1
if @?segid  eq 0  set segid  = PRO0
if @?openmm eq 0  set openmm = 1
if @?gb     eq 0  set gb     = gbmv
if @?nstep  eq 0  set nstep  = 1000

! Optimized GBMV2/SA force fields (K. Lee and J. Chen, JCC, 2017)
! ------------------------------------------------------------------
if @?TOPFilename eq 0 set TOPFilename  = top_all36_prot_wation.rtf
if @?PARFilename eq 0 set PARFilename  = par_all36_prot_gbmvop.prm
if @?GBRAdiiname eq 0 set GBRAdiiname  = radii_c36gbmvop.str

! Open and read the force fields files
!---------------------------------------------------------------
open unit 10 read form name @{TOPFilename}
read  rtf card unit 10
close unit 10
open unit 10 read form name @{PARFilename}
read para card unit 10
close unit 10

! Reading the PDB file and coordinates
!---------------------------------------------------------------
open read card unit 10 name @{pdbid}.pdb
read sequence pdb unit 10
generate @segid setup
read coordinate pdb unit 10 resid
close unit 10

! Nonbond cutoff
!---------------------------------------------------------------
NBOND atom switch cdie vdw vswitch -
   ctonnb 16.0 ctofnb 18.0 cutnb 20.0

! GB implicit solvent model?
!---------------------------------------------------------------
set wmain radii
stream @GBRAdiiname
! setup the input radii for GBMV calculations.
if gb eq gbmv  then
  GBMV BETA -12  P3 0.65 WATR 1.4  SHIFT -0.102 SLOPE 0.9085  P6 8 SA 0.005 -
       WTYP 2 NPHI 26 CUTNUM 50 KAPPA 0.0 weight
  ! parameter descriptions:
  ! https://www.charmm.org/charmm/documentation/by-version/c40b1/params/doc/gbmv/
  ! Notes:
  ! 1) CUTNUM (integer; recommend value >= 50; default value = 50)
  ! This value describes the cutoff length of lookup table array at each grid point,
  ! the larger the value is, the larger the size (computational memory) of lookup table array is,
  ! but there is a little effect on the computational cost.
  ! 2) KAPPA (positive real; default value = 0)
  ! This value has the inverse meaning as that of GBSW parameter.
endif
if gb eq gbmf  then
  GBMV BETA -12  P3 0.65 WATR 1.4  SHIFT -0.102 SLOPE 0.9085  P6 8 SA 0.005 -
       WTYP 2 NPHI 26 weight -
       FAST 1 SGBFRQ 4 SXD 0.3
endif

! OPENMM (it must be after GBMV setting)
!---------------------------------------------------------------
if openmm eq 1 then
   set platform = CUDA
   omm platform @platform precision single deviceid 0
   omm on
endif

! Energy calculation and print out the molecular forces
!---------------------------------------------------------------
energy
coor force comp
print coor comp

! NVE simulation
!---------------------------------------------------------------
dynamics leap start timestep 0.001 nstep @nstep -
         iseed 234234399 -
         nprint 100 iprfrq 100 isvfrq 100 -
         inbfrq -1   imgfrq -1   ihbfrq 0     ilbfrq  100 -
         iunread -1  iunwri -1  iuncrd -20   iunvelo -1  -
         nsavc 100   nsavv 0     iasvel 1 -
         tstruct 300 firstt 300  FINALT 300

STOP

